################################################################################
#
# RevBayes Example: Total-evidence dating under the fossilized birth-death model
#
# This file: Runs the full MCMC ...
#
# authors: Tracy A. Heath, Walker C. Pett, April M. Wright
#
################################################################################

#######################
# Reading in the Data #
#######################
# Create the full list of taxa (including all fossils and extant species #
taxa <- readTaxonData("data/taxa.tsv")

# Import the molecular sequences #
mol <- readDiscreteCharacterData("data/genetic_data.nxs")


# Import the morphological character matrix #
morpho <- readDiscreteCharacterData("data/phenotypic_asr_data.nxs")

# Add the missing taxa to each data partition #
mol.addMissingTaxa(taxa)
morpho.addMissingTaxa(taxa)

## helpers
moves = VectorMoves()
monitors = VectorMonitors()

n_taxa <- taxa.size()
n_branches <- 2 * n_taxa - 2
# Load the model files
source("scripts/model_FBDP_TEFBD_subfamily.Rev") # FBD tree prior

source("scripts/model_UExp_TEFBD.Rev") # UExp relaxed clock

source("scripts/model_GTRG_TEFBD.Rev") # Molecular substitution model (GTR+G)

source("scripts/model_Morph_TEFBD.Rev") # Morphological character change model


########
# MCMC #
########

# initialize the model object #
mymodel = model(rates_mol)
# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="output/clade_const.log", printgen=10))
# 2. the tree #
monitors.append( mnFile(filename="output/clade_const.trees", printgen=10, fbd_tree))
# 3. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=10,  num_samp_anc, origin_time))
#monitors.append( mnScreen(printgen=10)
# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves)

# Run the MCMC #
mymcmc.run(generations=200000)

# Quit RevBayes #
q()
